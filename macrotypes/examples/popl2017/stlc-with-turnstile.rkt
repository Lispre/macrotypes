#lang turnstile/lang

(define-type-constructor → #:arity > 0)

;; figure 11 #%app
;; uncomment to use this version, 
;; which does not special case the err msg for arity mismatch
;(define-typerule (#%app e_fn e_arg ...) ≫
;  [⊢ e_fn ≫ e_fn- ⇒ (~→ τ_in ... τ_out)]
;  [⊢ e_arg ≫ e_arg- ⇐ τ_in] ...
;  --------
;  [⊢ (#%app- e_fn- e_arg- ...) ⇒ τ_out])

;; #%app extended with improved arity err msg
(define-typed-syntax (#%app e_fn e_arg ...) ≫
  [⊢ e_fn ≫ e_fn- ⇒ (~→ τ_in ... τ_out)]
  #:fail-unless (stx-length=? #'[τ_in ...] #'[e_arg ...])
                (num-args-fail-msg #'e_fn #'[τ_in ...] #'[e_arg ...])
  [⊢ e_arg ≫ e_arg- ⇐ τ_in] ...
  --------
  [⊢ (#%app- e_fn- e_arg- ...) ⇒ τ_out])

(define-typerule (λ ([x:id (~datum :) τ_in:type] ...) e) ≫
   [[x ≫ x- : τ_in.norm] ... ⊢ e ≫ e- ⇒ τ_out]
   -------
   [⊢ (λ- (x- ...) e-) ⇒ (→ τ_in.norm ... τ_out)])
